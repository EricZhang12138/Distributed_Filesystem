cmake_minimum_required(VERSION 3.10)
project(AFS_v1)
set(CMAKE_CXX_STANDARD 17)


add_definitions(-D_FILE_OFFSET_BITS=64)
# 1. Dependencies
find_package(gRPC REQUIRED)
find_package(Protobuf REQUIRED) 
find_package(PkgConfig REQUIRED)
find_package(Boost REQUIRED)
pkg_check_modules(FUSE REQUIRED fuse)

# 2. File Generation Setup
set(PROTO_DIR "${CMAKE_CURRENT_SOURCE_DIR}/Basic_Operation/proto_files")
set(PROTO_FILE "${PROTO_DIR}/afs_operation.proto")

find_program(GRPC_CPP_PLUGIN grpc_cpp_plugin)
find_program(GRPC_PYTHON_PLUGIN grpc_python_plugin)
if(NOT GRPC_PYTHON_PLUGIN)
    message(FATAL_ERROR "grpc_python_plugin not found")
endif()

# Define ALL generated files (Both standard Proto and gRPC)
set(PROTO_SRCS "${CMAKE_CURRENT_BINARY_DIR}/afs_operation.pb.cc")
set(PROTO_HDRS "${CMAKE_CURRENT_BINARY_DIR}/afs_operation.pb.h")
set(GRPC_SRCS "${CMAKE_CURRENT_BINARY_DIR}/afs_operation.grpc.pb.cc")
set(GRPC_HDRS "${CMAKE_CURRENT_BINARY_DIR}/afs_operation.grpc.pb.h")
set(PROTO_PY "${CMAKE_CURRENT_BINARY_DIR}/afs_operation_pb2.py")
set(GRPC_PY "${CMAKE_CURRENT_BINARY_DIR}/afs_operation_pb2_grpc.py")

# Generate the codes for grpc
add_custom_command(
    OUTPUT ${PROTO_SRCS} ${PROTO_HDRS} ${GRPC_SRCS} ${GRPC_HDRS} ${PROTO_PY} ${GRPC_PY}
    COMMAND protobuf::protoc
    ARGS --grpc_out="${CMAKE_CURRENT_BINARY_DIR}"
         --cpp_out="${CMAKE_CURRENT_BINARY_DIR}"
         --python_out="${CMAKE_CURRENT_BINARY_DIR}"        # Add Python message classes
         --grpc_python_out="${CMAKE_CURRENT_BINARY_DIR}"   # Add Python gRPC stubs
         --plugin=protoc-gen-grpc="${GRPC_CPP_PLUGIN}"
         --plugin=protoc-gen-grpc_python="${GRPC_PYTHON_PLUGIN}"  # Python gRPC plugin
         -I "${PROTO_DIR}"                       # this command runs relative to the path of the Makefile generated by cmakelists.txt (CMAKE_CURRENT_BINARY_DIR)
         "${PROTO_FILE}"
    COMMAND ${CMAKE_COMMAND} -E touch "${CMAKE_CURRENT_BINARY_DIR}/__init__.py"
    DEPENDS ${PROTO_FILE}
)

# 3. Client Executable
add_executable(afs_client
    Basic_Operation/client_code/filesystem_client.cpp                # These are relative to the path of the CMakelists.txt
    FUSE/FUSE_integration.cpp
    ${PROTO_SRCS}
    ${GRPC_SRCS}
)

target_include_directories(afs_client PRIVATE
    ${CMAKE_CURRENT_BINARY_DIR}            # To find generated .pb.h files
    ${FUSE_INCLUDE_DIRS}                   # To find fuse3 headers
    Basic_Operation/client_code            # To find filesystem_client.hpp
    "${CMAKE_CURRENT_BINARY_DIR}/Basic_Operation/proto_files"
)

target_link_libraries(afs_client
    gRPC::grpc++
    protobuf::libprotobuf
    ${FUSE_LIBRARIES}
    Boost::boost
)

# 4. Server Executable
add_executable(afs_server
    Basic_Operation/server_code/filesystem_server.cpp
    ${PROTO_SRCS}
    ${GRPC_SRCS}
)

target_include_directories(afs_server PRIVATE
    ${CMAKE_CURRENT_BINARY_DIR}
    Basic_Operation/server_code            # To find filesystem_server.hpp
    "${CMAKE_CURRENT_BINARY_DIR}/Basic_Operation/proto_files"
)

target_link_libraries(afs_server
    gRPC::grpc++
    protobuf::libprotobuf
)

# 5. Test Filesystem 1
add_executable(client_test
    Basic_Operation/Operation_Test.cpp
    Basic_Operation/client_code/filesystem_client.cpp
    ${PROTO_SRCS}
    ${GRPC_SRCS})

target_include_directories(client_test PRIVATE
    Basic_Operation/client_code
    ${CMAKE_CURRENT_BINARY_DIR}            # To find generated .pb.h files
    "${CMAKE_CURRENT_BINARY_DIR}/Basic_Operation/proto_files"
)

target_link_libraries(client_test
    gRPC::grpc++
    protobuf::libprotobuf)

# 6. Test Filesystem 2
add_executable(register_test
    Basic_Operation/client_test.cpp
    Basic_Operation/client_code/filesystem_client.cpp
    ${PROTO_SRCS}
    ${GRPC_SRCS}
)

target_include_directories(register_test PRIVATE
    Basic_Operation/client_code
    ${CMAKE_CURRENT_BINARY_DIR}            # To find generated .pb.h files
    "${CMAKE_CURRENT_BINARY_DIR}/Basic_Operation/proto_files"
)

target_link_libraries(register_test
    gRPC::grpc++
    protobuf::libprotobuf)
